plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.139'
    id 'idea'
}

version = mod_version
group = mod_group_id

repositories {
    // Impact Maven - Cobblemon repository
    maven {
        name = "Impact Maven"
        url = "https://maven.impactdev.net/repository/development/"
    }
    // CurseForge Maven - for KotlinForge
    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
    kotlinForgeRuntime
}

dependencies {
    // Cobblemon
    compileOnly("com.cobblemon:neoforge:1.7.2+1.21.1")
    localRuntime("com.cobblemon:neoforge:1.7.2+1.21.1")

    // CobbleDollars - manual JAR in repo libs/
    compileOnly fileTree(dir: "libs", include: ["cobbledollars-*.jar"])
    localRuntime fileTree(dir: "libs", include: ["cobbledollars-*.jar"])

    // KotlinForge (required for Cobblemon)
    kotlinForgeRuntime "curse.maven:kotlin-for-forge-351264:5978393"
    localRuntime "curse.maven:kotlin-for-forge-351264:5978393"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_description        : mod_description,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.register('copyKotlinForgeToRunMods', Copy) {
    from configurations.kotlinForgeRuntime
    into layout.projectDirectory.dir('run/mods')
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
tasks.named('runClient').configure { dependsOn 'copyKotlinForgeToRunMods' }
tasks.named('runServer').configure { dependsOn 'copyKotlinForgeToRunMods' }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
